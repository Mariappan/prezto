#
# A minimal two-color theme.
#
# Authors:
#   Kevin Laude <nerfyoda@gmail.com>
#
# Features:
#   - One line, left aligned.
#   - The prompt is prefixed by a character sequence of your choice.
#   - Only displays the current directory instead of the full path.
#   - Displays the current branch when in a git project (this requires loading
#     the git module before prompt in ~/.zpreztorc).
#   - Displays a character at the end of the prompt when in a git project with
#     "dirty" files.
#
# Usage:
#   This prompt's prefix symbol and colors are customizable:
#     prompt mario [<symbol>] [<color1>] [<color2>]
#
#   In ~/.zpreztorc:
#     zstyle ':prezto:module:prompt' theme 'mario' \
#       ['<symbol>'] \
#       ['<color1>'] \
#       ['<color2>']
#
#   If these options are not provided, the symbol defaults to "☁" with colors
#   cyan and green.
#
# Screenshots:
#   http://i.imgur.com/mJCZ8rE.png
#
# Note:
#   This is a port of the oh-my-zsh mario theme, originally written by Phillip
#   Ridlen <p@rdln.net> and Mark Drago <markdrago@gmail.com>
#

# Load dependencies.
pmodload 'helper'

function prompt_mario_pwd {
  local pwd1="${PWD/#$HOME/~}"
  local pwd2="${${pwd1}/\/work\/swrouting01_3\/rmariappan/~/work}"
  local pwd="${${pwd2}/\/neteng\/rmariappan/~/neteng}"

  _prompt_mario_pwd="$pwd"
}


function prompt_mario_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Replace '/home/<user>' with '~'.
  prompt_mario_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
      git-info
  fi
}

function prompt_mario_help {
  cat <<EOT
This prompt's prefix symbol and colors are customizable:

  prompt mario [<symbol>] [<color1>] [<color2>]

In ~/.zpreztorc:
  zstyle ':prezto:module:prompt' theme 'mario' ['<symbol>'] ['<color1>'] ['<color2>']

If these options are not provided, the symbol defaults to ☁ with colors cyan
and green.
EOT
}

function prompt_mario_preview {
  if (( $# > 0 )); then
    prompt_preview_theme 'mario' "$@"
  else
    prompt_preview_theme 'mario'
    print
    prompt_preview_theme 'mario' ">" "yellow" "red"
  fi
}

function prompt_mario_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Set the theme prefix to a mario or to the user's given characters.
  if [[ -n "$1" ]]; then
    prefix="$1"
  else
    prefix='☁'
    prefix='λ'
  fi

  # Assign colors.
  if [[ -n "$2" ]]; then
    primary_color="$2"
  else
    primary_color='cyan'
  fi

  if [[ -n "$3" ]]; then
    secondary_color="$3"
  else
    secondary_color='green'
  fi

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_mario_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '<%s>'
  zstyle ':prezto:module:git:info:added' format ' +%a'
  zstyle ':prezto:module:git:info:ahead' format ' %F{red}↑%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{red}↓%f'
  zstyle ':prezto:module:git:info:branch' format "%%B%F{$secondary_color}[%f%%b%%B%F{$primary_color}  %b%f%%b"
  zstyle ':prezto:module:git:info:clean' format "%B%F{$secondary_color}]%f%b"
  zstyle ':prezto:module:git:info:commit' format '➦ %.7c'
  zstyle ':prezto:module:git:info:deleted' format ' -%d'
  zstyle ':prezto:module:git:info:dirty' format "%B%F{$secondary_color}]%f%b"
  zstyle ':prezto:module:git:info:modified' format ' ✱%m'
  zstyle ':prezto:module:git:info:renamed' format ' ➙%r'
  zstyle ':prezto:module:git:info:untracked' format ' ?%u'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%b%a%d%m%r%u%C%D%s%A%B'
    #'rprompt' '%A%B'

  #zstyle ':prezto:module:git:info:position' format '%p'
  #zstyle ':prezto:module:git:info:stashed' format ' S'
  #zstyle ':prezto:module:git:info:unmerged' format ' ═'

  # Define prompts.
  PROMPT='%B%F{$primary_color}${prefix}%f%b  %B%F{$secondary_color}%c%f%b ${git_info:+${(e)git_info[prompt]}} » '
  RPROMPT='%B%F{$secondary_color}${_prompt_mario_pwd}%f%b'
}

prompt_mario_setup "$@"
